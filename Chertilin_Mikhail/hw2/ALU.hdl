// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=xZeroed);
    Mux16(a=y, b=false, sel=zy, out=yZeroed);

    Not16(in=xZeroed, out=notX);
    Mux16(a=xZeroed, b=notX, sel=nx, out=xProcessed);

    Not16(in=yZeroed, out=notY);
    Mux16(a=yZeroed, b=notY, sel=ny, out=yProcessed);

    And16(a=xProcessed, b=yProcessed, out=xAndY);
    Add16(a=xProcessed, b=yProcessed, out=xPlusY);
    Mux16(a=xAndY, b=xPlusY, sel=f, out=functionResult);

    Not16(in=functionResult, out=notFunctionResult);
    Mux16(a=functionResult, b=notFunctionResult, sel=no, out=out, 
          out=outFlag, out[0..7]=outFlagLower, out[8..15]=outFlagUpper, 
          out[15]=negativeFlag);

    Or8Way(in=outFlagLower, out=lowerOr);
    Or8Way(in=outFlagUpper, out=upperOr);

    Or(a=lowerOr, b=upperOr, out=notZero);
    Not(in=notZero, out=zr);
}