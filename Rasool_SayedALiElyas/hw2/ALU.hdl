// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x , b=false , sel=zx , out=forx);
    Not16(in=forx , out=notforx );
    Mux16(a=forx , b=notforx , sel=nx , out=forx1 );

    Mux16(a=y , b=false , sel=zy , out=fory);
    Not16(in=fory , out=notfory );
    Mux16(a=fory, b=notfory , sel=ny , out=fory1 );

    
    Add16(a=forx1 , b=fory1 , out=sumforx1y1);
    And16(a=forx1 , b=fory1 , out=andforx1y1 );

    Mux16(a=andforx1y1 , b=sumforx1y1 , sel=f , out= out0);
    Not16(in=out0 , out=notout0);
    

    
    
    Mux16(a=out0, b=notout0, sel=no, out=out,
                                   out=outflag,
                                   out[0..7]=outflag1,
                                   out[8..15]=outflag2,
                                   out[15]=ng);

    Or8Way(in=outflag1, out=o1);
    Or8Way(in=outflag2, out=o2);
    Or(a=o1, b=o2, out=o3);
    Not(in=o3, out=zr);

    


    
    
    
}